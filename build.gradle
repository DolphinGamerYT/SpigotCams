plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.1'
}

group = 'SpigotCams'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://jitpack.io' }

}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'

    implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT'
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.4'
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    classifier = ''
    relocate 'co.aikar.commands', 'shadded.acf'
    relocate 'co.aikar.locales', 'shadded.acf.locales'
    minimize();
}

jar {
    classifier = "original"
}

tasks.build.dependsOn tasks.shadowJar
